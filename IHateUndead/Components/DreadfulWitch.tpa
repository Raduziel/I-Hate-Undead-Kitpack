ADD_KIT ~RAWI~
    ~RAWI 0 1 1 1 1 0 1 1~
    ~RAWI 0 1 1 1 1 0 1 1 0 0 0 1 0 0 0 1 0 1 1 0 0 0 1 0 0 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAWI 3 3 12 3 12 3~
	~RAWI 0 0 0 0 0 0~
	~RAWI 99 99 99 99 99 99~
    ~RAWI 99 99 99 99 99 99~
	~RAWI 0 0 0 0 0 1 0 0 0~
	~RAWI 0 0 0 0 0 0~
	~IHateUndead\Tables\RAWI.2da~
	~K_Sh_H     K_Sh_HE     K_Sh_HO~
	~0x00004000 21~
	~Sh0~
	~LEAT21 * * * RING22 RING39 CLCK27 BOOT04 AMUL21 BRAC18 * * * * * * * DAGG12 BOW19 STAF10~
	SAY @1001
	SAY @1002
	SAY @1003

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAWI~
		hpclass = ~HPRAWi~
		clascolr = ~220 7 41 164 220~
END

ACTION_INCLUDE ~IHateUndead/lib/fnp_compat.tpa~
ACTION_INCLUDE ~IHateUndead/lib/remove_lines.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 0
	u_maces 	 		= 0
	u_flails 			= 0
	u_axes 				= 1
	u_daggers 			= 1
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 1
	u_darts 			= 1
	u_slings	 		= 1
	u_bows 				= 1
	u_crossbows 		= 0
  STR_VAR
    clab_name 	    = ~RAWI~
	class 			= ~shaman~	 
	s_Life 			= ~minor~
	s_Death 		= ~x~
	s_Benediction 	= ~x~
	s_Destruction 	= ~x~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~major~
	s_Vigor 		= ~x~
	s_Affliction 	= ~major~
	s_Animal 		= ~minor~
	s_Plant 		= ~minor~
	s_Earth 		= ~major~
	s_Water 		= ~minor~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~major~
	s_Magic 		= ~x~
	s_Perdition     = ~major~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "IHateUndead\Spl\RAWIFE.spl" override

COPY "IHateUndead\Spl\RACUMI.spl" override	SAY NAME1 @1004	SAY UNIDENTIFIED_DESC @1004

COPY "IHateUndead\Spl\RACUTE.spl" override	SAY NAME1 @1005	SAY UNIDENTIFIED_DESC @1005

COPY "IHateUndead\Spl\RACURA.spl" override	SAY NAME1 @1006	SAY UNIDENTIFIED_DESC @1006

COPY "IHateUndead\Spl\RACUMA.spl" override	SAY NAME1 @1007	SAY UNIDENTIFIED_DESC @1007

COPY "IHateUndead\Spl\RACUVO.spl" override	SAY NAME1 @1008	SAY UNIDENTIFIED_DESC @1008

COPY "IHateUndead\Spl\RACUAG.bam" override

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@1011) STR_VAR bam_file = RACUAG RET icon END
COPY "IHateUndead\Spl\RACUAG.spl" override	SAY NAME1 @1009	SAY UNIDENTIFIED_DESC @1009
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = icon END

COPY "IHateUndead\Spl\RACUDE.spl" override	SAY NAME1 @1010	SAY UNIDENTIFIED_DESC @1010

COPY "IHateUndead\Tables\HPRAWi.2da" override

ACTION_FOR_EACH file IN ~RADWAG~ ~RADWDE~ ~RADWMA~ ~RADWMI~ ~RADWRA~ ~RADWTE~ ~RADWVO~ BEGIN
  COPY ~IHateUndead\Bam\DreadfulWitch\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~IHateUndead\Bam\DreadfulWitch\MOS%original_base_index%.pvrz~
    END
  END
END

//EoF