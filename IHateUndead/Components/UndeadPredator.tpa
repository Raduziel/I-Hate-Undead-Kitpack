ADD_KIT ~RAUP~
    ~RAUP 1 1 1 1 1 1 1 1~
    ~RAUP 0 0 0 0 0 0 0 0 0 2 0 2 0 0 0 0 2 2 2 0 0 2 2 2 0 0 0 0 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAUP 13 13 14 3 16 3~
	~RAUP 0 0 0 0 0 -2~
	~RAUP 99 99 99 99 99 99~
    ~RAUP 15 17 0 0 15 0~
	~RAUP 0 0 0 1 0 0 0 0 0~
	~RAUP 0 0 0 0 0 0~
	~IHateUndead\Tables\RAUP.2da~
	~K_R_H	K_R_D   K_R_G   K_R_E   K_R_HE   K_R_HL   K_R_HO~
	~0x00010000 12~
	~Ra0~
	~LEAT19 BLUN25 HELM16 * RING27 RING31 CLCK02 BOOT01 AMUL27 BRAC10 * * * * * * * SW1H31 AX1H10 STAF08~
	SAY @101
	SAY @102
	SAY @103

LAF fl#add_kit_ee
	INT_VAR										
		fallen_notice = RESOLVE_STR_REF (@106)
	STR_VAR
		kit_name = ~RAUP~		
		clascolr = ~6 160 164 242 163~
END

DEFINE_PATCH_MACRO ~PREDATOR_SPELL_TO_INNATE~ BEGIN                  //The original macro is from CamDawg, I just made some adjustments for this kit.

  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_ASCII (abil_off + 0x04) bam (8)                               // reads the bam filename from ability
  WRITE_SHORT 0x1C 4                                                 // sets spell type to innate (4)
  WRITE_LONG  0x34 1                                                 // sets spell level to 1 to avoid scripting issues
  WRITE_ASCIIE 0x3A "%bam%" #8                                       // writes the bam filename from abilities to spell icon
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    WRITE_SHORT (abil_off + 0x02 + (0x28 * index)) 4                 // changes ability icon location to innate (4)
	WRITE_SHORT (abil_off + 0x0C + (0x28 * index)) 1                 // changes ability target to creature (1)
	WRITE_LONG  (abil_off + 0x0E + (0x28 * index)) 1                 // changes ability range to touch (1)
    READ_SHORT  (abil_off + 0x12 + (0x28 * index)) speed             // reads casting speed
    PATCH_IF (speed > 3) BEGIN
      WRITE_SHORT  (abil_off + 0x12 + (0x28 * index)) (speed - 2)    // reduces casting speed
    END ELSE BEGIN
      WRITE_SHORT  (abil_off + 0x12 + (0x28 * index)) 0              // makes instant
    END
  END

END

COPY "IHateUndead\Spl\RAUPFE.spl" override
	LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Diseased%" END
	LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL "%RA_FoulDisease%" END

COPY_EXISTING ~SPPR108.spl~ ~override/RAUPRF.spl~
LPM PREDATOR_SPELL_TO_INNATE

COPY_EXISTING ~SPPR308.spl~ ~override/RAUPRP.spl~
LPM PREDATOR_SPELL_TO_INNATE

COPY_EXISTING ~SPPR317.spl~ ~override/RAUPCD.spl~
LPM PREDATOR_SPELL_TO_INNATE

COPY_EXISTING ~SPPR413.spl~ ~override/RAUPNP.spl~
LPM PREDATOR_SPELL_TO_INNATE

COPY_EXISTING ~SPPR417.spl~ ~override/RAUPLR.spl~
LPM PREDATOR_SPELL_TO_INNATE

COPY "IHateUndead\Spl\RAUPDU.spl" override	SAY NAME1 @104	SAY UNIDENTIFIED_DESC @105

COPY_EXISTING ~SPPR101.spl~ ~override\RAUPBR1.spl~
LPM I_HATE_UNDEAD_FEATURES
		
COPY_EXISTING ~SPPR107.spl~ ~override\RAUPBR2.spl~
LPM I_HATE_UNDEAD_FEATURES

COPY_EXISTING ~SPPR108.spl~ ~override\RAUPBR3.spl~
LPM I_HATE_UNDEAD_FEATURES

COPY_EXISTING ~SPPR413.spl~ ~override\RAUPBR4.spl~ 
LPM I_HATE_UNDEAD_FEATURES

COPY "IHateUndead\Spl\RAUPBR.spl" override

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 91 93 94 95 96 99 100 104 105 106 107  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAUP + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@102)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
		READ_ASCII	0x22 animation (2)
		PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
		  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUP~) parameter2 = 9 special = RESOLVE_STR_REF (@102) END
		END
		PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
		  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUP~) parameter2 = 9 special = RESOLVE_STR_REF (@102) END
		END
		END
	END
BUT_ONLY

// EE : Workaround to display the correct fallen status in character screens. Kindly provided by Gwendolyne.

ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
	ACTION_CLEAR_ARRAY GW_Fallen_Kits
	ACTION_DEFINE_ASSOCIATIVE_ARRAY GW_Fallen_Kits BEGIN
	107,	108	=> RAUP
	END
	ACTION_PHP_EACH GW_Fallen_Kits AS GW_kits => GW_kit_name BEGIN
		COPY_EXISTING - ~clastext.2da~ ~override~
			COUNT_2DA_COLS cols									// amount of columns
			READ_2DA_ENTRIES_NOW kittable cols					// read all file into memory  
			FOR (row = 1 ; row < kittable ; ++row) BEGIN		// iterate over rows
				READ_2DA_ENTRY_FORMER kittable row 0 ~kit_name~	// read column value
				PATCH_IF ~%kit_name%~ STRING_EQUAL_CASE ~%GW_kit_name%~ BEGIN
					READ_2DA_ENTRY_FORMER kittable row 1 kit_classid
					READ_2DA_ENTRY_FORMER kittable row 2 kit_kitid
					READ_2DA_ENTRY_FORMER kittable row 4 kit_desc
					READ_2DA_ENTRY_FORMER kittable row 6 kit_bio
					READ_2DA_ENTRY_FORMER kittable row 8 kit_briefdesc
					SET patch_lower = RESOLVE_STR_REF ((AT %GW_kits_0%))
					SET patch_mixed = RESOLVE_STR_REF ((AT %GW_kits_1%))
					SET patch_fallen = 1
					SET patch_fallen_notice = "-1"
					SPRINT patch_clastext "FALLEN_%GW_kit_name% %kit_classid% %kit_kitid% %patch_lower% %kit_desc% %patch_mixed% %kit_bio% %patch_fallen% %kit_briefdesc% %patch_fallen_notice%"
					i = kittable
					INNER_ACTION BEGIN APPEND clastext.2da "%patch_clastext%" UNLESS "FALLEN_%GW_kit_name%" END
				END
			END
	END
	ACTION_CLEAR_ARRAY GW_Fallen_Kits
END

COPY ~IHateUndead\Bam\UndeadPredator\RAUPDU.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~IHateUndead\Bam\UndeadPredator\MOS%original_base_index%.pvrz~
    END
  END

//EoF